plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libGroupId = "com.sd.lib.umeng"
def libArtifactId = "umeng_share"
def libVersionCode = 20210702
def libVersionName = "7.1.5-v01"

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode libVersionCode
        versionName libVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.aar'])
    api fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'

    //友盟公共库，必选
    api 'com.github.zj565061763:umeng_common:9.4.0-v01'

    //分享核心库，必选
    api 'com.umeng.umsdk:share-core:7.1.5'
    //分享面板功能，可选
    api 'com.umeng.umsdk:share-board:7.1.5'

    //微信完整版
    api 'com.umeng.umsdk:share-wx:7.1.5'
    //微信官方依赖库，必选
    api 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.7.0'

    //QQ完整版
    api 'com.umeng.umsdk:share-qq:7.1.5'
    //QQ官方依赖库，必选
    api 'com.tencent.tauth:qqopensdk:3.53.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'

    //新浪微博完整版
    api 'com.umeng.umsdk:share-sina:7.1.5'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}