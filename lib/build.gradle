plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libGroupId = "com.sd.lib.umeng"
def libArtifactId = "umeng_share"
def libVersionCode = 20210626
def libVersionName = "7.1.5-v1"

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode libVersionCode
        versionName libVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.aar'])
    api fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'

    //必选
    api 'com.umeng.umsdk:common:9.4.0'
    //必选
    api 'com.umeng.umsdk:asms:1.2.3'

    //分享核心库，必选
    implementation 'com.umeng.umsdk:share-core:7.1.5'
    //分享面板功能，可选
    implementation 'com.umeng.umsdk:share-board:7.1.5'

    //微信官方依赖库，必选
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.7.0'

    //QQ官方依赖库，必选
    implementation 'com.tencent.tauth:qqopensdk:3.53.0'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = libGroupId
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}